
(module cli G
  (defcap G () (enforce false "X"))

  (defschema cap
    name:string
    args:list
    signers:[string])

  (defschema signer
    signer:string
    signature:string)

  (defschema data
    sender:string
    ttl:integer
    network-id:string
    creation-time:time
    nonce:string
    host:string
    chain-id:integer
    chain-count:integer
    signers:[object{signer}]
    caps:[object{cap}]
    code:string
    )

  (defconst PACT_CHAIN_ID:integer -1
    "chain id of a pact -s server")

  (deftable state:{data})
  (defconst S "S")

  (defschema keydata
    file:string)

  (deftable keystore:{keydata})

  (defschema account
    id:string
    guard:guard)

  (deftable wallet:{account})

  (defun init ()
    (write
     state S
     { 'sender: "sender"
     , 'ttl: 150000
     , 'network-id: "mainnet01"
     , 'creation-time: (now)
     , 'nonce: "nonce"
     , 'host: "api.chainweb.com"
     , 'chain-id: 0
     , 'chain-count: 20
     , 'signers: []
     , 'caps: []
     , 'code: ""
     }))

  (defun sstore (name:string)
    (write state name (cli-state)))

  (defun sload(name:string)
    (write state S (read state name))
    (rehash))

  (defun cli-state () (read state S))

  (defun mainnet ()
    (network-id "mainnet01")
    (host "api.chainweb.com")
    (rehash))

  (defun testnet ()
    (network-id "testnet04")
    (host "us1.testnet.chainweb.com")
    (rehash))

  (defun devnet ()
    (network-id "development")
    (host "us1.tn1.chainweb.com")
    (rehash))

  (defun update-state (u:object rh:bool)
    (update state S u)
    (if rh (rehash) "State updated"))

  (defun sender (s:string)
    (update-state { 'sender: s } true))

  (defun ttl (i:integer)
    (update-state { 'ttl: i } true))

  (defun network-id (s:string)
    (update-state { 'network-id: s } true))

  (defun creation-time (s:string)
    (update-state { 'creation-time: (time s) } true))

  (defun nonce (s:string)
    (update-state { 'nonce: s } true))

  (defun host (h:string)
    (update-state { 'host: h } false))

  (defun chain-id (i:integer)
    (update-state { 'chain-id: i } true))

  (defun set-code (s:string)
    (update-state { 'code: s } true))

  (defun add-key (id:string key:object{keydata})
    (write keystore id key))

  (defun add-key1 (key:string)
    (add-key key { 'file: "" }))

  (defun add-keyfile1 (key:string file:string)
    (add-key key { 'file: file })
    (rehash)
    (format "Added keyfile for key {}" [key]))

  (defun get-key:object{keydata} (key:string)
    (read keystore key))

  (defun add-cap1 (name:string args:list signers:[string])
    (let ((ss (map (add-signer) signers)))
      (update-state
       { 'caps:
         (+ [{ 'name: name
             , 'args: args
             , 'signers: ss }]
            (at 'caps (read state S)))}
       true)))

  (defun sign (signer:string signature:string)
    (let ((s (add-signer signer)))
      (update-state
       { 'signers:
         (map (sign1 s signature)
               (at 'signers (cli-state))) } true)))

  (defun sign1 (signer:string signature:string s:object{signer})
    (if (= signer (at 'signer s))
        (+ { 'signature: signature} s)
      s))


  (defun add-signer-unsafe (s:string sig:string)
    (let ((ss (at 'signers (read state S))))
      (if (contains s (map (at 'signer) ss)) "Already added"
        (update-state
         { 'signers: (+ [{'signer:s,'signature:sig}] ss) } true))))

  (defun add-signer (signer:string)
    "Matches SIGNER to keystore to add to signers list."
    (let ((matched (fold (match-key signer) "" (keys keystore))))
      (enforce (!= "" matched) "Key not found")
      (add-signer-unsafe matched "")
      matched))

  (defun clear-sigs ()
    (update-state
     { 'signers:
       (map (+ { 'signature: "" })
        (at 'signers (cli-state))) }
     false))

  (defun match-key (part:string r:string key:string)
    (if (= part (take (length part) key))
        (let ((x 0))
          (enforce (= "" r) "Multiple matches found")
          key)
      r))


)
(create-table state)
(create-table keystore)
(init)
(env-gaslimit 5000)
(env-gasprice 0.00000000001)
